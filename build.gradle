import java.time.Duration

// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    repositories {
        mavenCentral()
        google()
    }
    dependencies {
        classpath("com.android.tools.build:gradle:4.1.3")

        // For displaying method/field counts when building with Gradle:
        // https://github.com/KeepSafe/dexcount-gradle-plugin
        classpath("com.getkeepsafe.dexcount:dexcount-gradle-plugin:2.0.0")

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

plugins {
    id("java")
    id("io.github.gradle-nexus.publish-plugin") version "1.1.0"
}

// Must be specified in root project for the gradle nexus publish plugin.
group = "com.launchdarkly"
// Specified in the root project so Releaser's `publish-dry-run.sh` can see it in `./gradlew properties`
archivesBaseName = 'launchdarkly-android-client-sdk'
// Specified in gradle.properties
version = version

allprojects {
    repositories {
        mavenLocal()
        // Before LaunchDarkly release artifacts get synced to Maven Central they are here along with snapshots:
        maven { url = uri("https://oss.sonatype.org/content/groups/public/") }
        google()
        mavenCentral()
    }
}

subprojects {
    afterEvaluate {
        configure(android.lintOptions) {
            abortOnError = false
        }
        configure(android.compileOptions) {
            sourceCompatibility = JavaVersion.VERSION_1_8
            targetCompatibility = JavaVersion.VERSION_1_8
        }
        gradle.projectsEvaluated {
            tasks.withType(JavaCompile) {
                // enable deprecation checks
                options.compilerArgs << "-Xlint:deprecation"
            }
        }
    }
}

nexusPublishing {
    packageGroup = "com.launchdarkly"
    repositories {
        sonatype {
            nexusUrl.set(uri("https://oss.sonatype.org/service/local/staging/deploy/maven2/"))
            snapshotRepositoryUrl.set(uri("https://oss.sonatype.org/content/repositories/snapshots/"))
        }
    }

    transitionCheckOptions {
        maxRetries.set(20)
        delayBetween.set(Duration.ofMillis(3000))
    }
}