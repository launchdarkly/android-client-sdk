import java.time.Duration
import com.android.build.gradle.BaseExtension

// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    repositories {
        mavenCentral()
        google()
    }
    dependencies {
        classpath('com.android.tools.build:gradle:8.3.2')

        // For displaying method/field counts when building with Gradle:
        // https://github.com/KeepSafe/dexcount-gradle-plugin
        classpath("com.getkeepsafe.dexcount:dexcount-gradle-plugin:4.0.0")

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

plugins {
    id("java")
    id("io.github.gradle-nexus.publish-plugin") version "1.3.0"
}

// Must be specified in root project for the gradle nexus publish plugin.
group = "com.launchdarkly"
base {
    // Specified in the root project so Releaser's `publish-dry-run.sh` can see it in `./gradlew properties`
    archivesName = "launchdarkly-android-client-sdk"
}
// Specified in gradle.properties
version = version

allprojects {
    repositories {
        mavenLocal()
        // Before LaunchDarkly release artifacts get synced to Maven Central they are here along with snapshots:
        maven { url = uri("https://oss.sonatype.org/content/groups/public/") }
        google()
        mavenCentral()
    }
}

def configureAndroidModule(Project project) {
    project.extensions.configure(BaseExtension) { androidExt ->
        androidExt.lint { abortOnError = false }
        androidExt.compileOptions {
            sourceCompatibility = JavaVersion.VERSION_1_8
            targetCompatibility = JavaVersion.VERSION_1_8
        }
    }
}

nexusPublishing {
    packageGroup = "com.launchdarkly"
    repositories {
        sonatype{
            nexusUrl.set(uri("https://ossrh-staging-api.central.sonatype.com/service/local/"))
            snapshotRepositoryUrl.set(uri("https://central.sonatype.com/repository/maven-snapshots/"))
        }
    }
}

subprojects { subproject ->
    plugins.withId("com.android.application") {
        configureAndroidModule(subproject)
    }
    plugins.withId("com.android.library") {
        configureAndroidModule(subproject)
    }
    tasks.withType(JavaCompile).configureEach {
        // enable deprecation checks
        options.compilerArgs += "-Xlint:deprecation"
    }
}
