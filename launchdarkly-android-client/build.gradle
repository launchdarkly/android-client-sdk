apply plugin: 'com.android.library'
apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'org.ajoberstar.github-pages'
// make sure this line comes *after* you apply the Android plugin
apply plugin: 'com.getkeepsafe.dexcount'

allprojects {
    group = 'com.launchdarkly'
    version = '2.0.0-SNAPSHOT'
    sourceCompatibility = 1.7
    targetCompatibility = 1.7
}

android {
    compileSdkVersion 24
    buildToolsVersion '25.0.2'

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 24
        versionCode 1
        versionName version
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    lintOptions {
        // TODO: fix things and set this to true
        abortOnError false
    }

    configurations {
        javadocDeps
    }

    packagingOptions {
        exclude 'META-INF/**'
        exclude 'META-INF/**'
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.google.guava:guava:20.0'
    compile 'com.noveogroup.android:android-logger:1.3.5'
    compile 'com.google.code.gson:gson:2.7'
    compile 'com.launchdarkly:okhttp-eventsource:1.1.1'
    compile 'com.squareup.okhttp3:okhttp:3.6.0'

    androidTestCompile 'com.android.support:appcompat-v7:24.2.1'
    androidTestCompile 'com.android.support:support-annotations:24.2.1'
    androidTestCompile 'com.android.support.test:runner:0.5'
    androidTestCompile 'com.android.support.test:rules:0.5'
    androidTestCompile 'org.hamcrest:hamcrest-library:1.3'
    androidTestCompile 'org.easymock:easymock:3.4'
    androidTestCompile 'com.google.dexmaker:dexmaker:1.1'
    androidTestCompile 'junit:junit:4.12'

    javadocDeps 'com.google.code.gson:gson:2.7'
    javadocDeps 'com.squareup.okhttp3:okhttp:3.6.0'
    javadocDeps 'com.launchdarkly:okhttp-eventsource:1.1.1'
}

repositories {
    mavenLocal()
    // Before LaunchDarkly release artifacts get synced to Maven Central they are here along with snapshots:
    maven { url "https://oss.sonatype.org/content/groups/public/" }
    mavenCentral()
}

buildscript {
    repositories {
        jcenter()
        mavenCentral()
        mavenLocal()
    }
    dependencies {
        classpath 'org.ajoberstar:gradle-git:1.5.0-rc.1'
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.3'
        classpath 'com.getkeepsafe.dexcount:dexcount-gradle-plugin:0.6.3'
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    failOnError false
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    classpath += configurations.javadocDeps
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

githubPages {
    repoUri = 'https://github.com/launchdarkly/android-client.git'
    pages {
        from javadoc
    }
    credentials {
        username = githubUser
        password = githubPassword
    }
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'LaunchDarkly SDK for Android'
                packaging 'jar'
                description 'Official LaunchDarkly SDK for Android'
                url 'https://github.com/launchdarkly/android-client'

                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id 'drichelson'
                        name 'Dan Richelson'
                        email 'dan@launchdarkly.com'
                    }
                }

                scm {
                    connection 'scm:git:git://github.com/launchdarkly/android-client.git'
                    developerConnection 'scm:git:ssh:git@github.com:launchdarkly/android-client.git'
                    url 'https://github.com/launchdarkly/android-client'
                }
            }
        }
    }
}